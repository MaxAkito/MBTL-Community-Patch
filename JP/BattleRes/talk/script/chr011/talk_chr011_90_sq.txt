
Talk <-  // 何を追加してもいいが Finalize() で関数のみになること
{
	IsDone = 0, // ロードできたか
	Count = 0,
	Show = 0, // 描画が有効かどうか
};

// はじまり
function Talk::Initialize()
{
	local p = Talk;
	local fnc = tPAni;

	p.IsDone = 1; // とりあえず成功にしておく

	// 枠生成(ファイル単位)
	p.PA_Ed <- tPAni.CDat();
	p.PA_Manager <- fnc.CAniManager(); // マネージャ生成

	// PAniデータ読み込み
	local sFilePath = "./BattleRes/talk/st_pat";
	
	local sPcFile = format( "%s/chr011/chr011_ed00.pat", sFilePath );
	TalkSqCallback.UseFileInSquirrel( {type=0, file_name=sPcFile} );
	if( p.PA_Ed.Load( sPcFile ) == 0 )
	{
		//ロード失敗
		print( format( "\n >> %s Load error.", sPcFile ) );
		p.IsDone = 0;
	}
	else
	{
		//ロード成功
		p.Show = 1;//描画を有効にする
	}
}

// 終了処理
function Talk::Finalize()
{
	local p = Talk;
	local fnc = tPAni;

	// アニメマネージャの解放、登録した中身も全部消える
	delete p.PA_Manager;

	// PAniオブジェクトの解放
	delete p.PA_Ed;

	delete Talk;

	print("\nTalk::Finalize()");
}

// 会話スクリプトからコールされる
function Talk::Call(call_no)
{
	print( format( "\nCall Call:%d", call_no ) );


	local prio = 400; // 全体プライオリティ

	local p = Talk;
	local fnc = tPAni;
	if( p.IsDone != 0 )
	{
		local framestep = 1.0/60; // 1フレーム
		local fWait = 0.0;
		local fr = 0;
	
		// フレーム登録
		
		// イラスト本体
		fr = p.PA_Manager.Add( p.PA_Ed, "bg" );
		fr.SetPrio( prio );
		fWait = framestep * 30;
		for(local i = 0;i <= 5;i++ )
		{
			local s = format( "bg%03d", i );
			fr.AddFrame( s, fWait );
		}
		fr.AddTag("loop")
		fWait = framestep * 90;
		for(local i = 100;i <= 111;i++ )
		{
			local s = format( "bg%03d", i );
			fr.AddFrame( s, fWait );
		}
		fr.AddTagJump("loop");
	}

}

// 会話スクリプトからウェイト(アニメ終了待ち等の)問い合わせ
function Talk::Wait(call_no)
{
	print( format( "\nTalk Wait:%d", call_no ) );
	return( 0 );	// 0でウェイト終了、それ以外の値でウェイト状態をキープ
}


// スタッフロールが表示される前(ED会話が終わった)のタイミングでコールされる
function Talk::GotoStaffRoll()
{
	print( format( "\nTalk GotoStaffRoll" ) );
	
	local p = Talk;
	p.Show = 0; // 描画を無効にする
}

// 最後の一枚絵が表示される前(スタッフロールが終わった)のタイミングでコールされる
function Talk::GotoEndingLastCG()
{
	print( format( "\nTalk GotoEndingLastCG" ) );
	
	local p = Talk;
	p.Show = 0; // 描画を無効にする
}


//
function Talk::Update()
{
	local p = Talk;
	local fnc = tPAni;

	if( p.Show )
	{
		p.PA_Manager.Update();
		p.Count++;
	}
}

//
function Talk::Render(add_x, add_y)
{
	local p = Talk;
	local fnc = tPAni;
	
	if( p.Show )
	{
		local pa_cx = 1280/2;
		local pa_cy = 720/2;

		p.PA_Ed.SetPosition( pa_cx+add_x, pa_cy+add_y );
		p.PA_Manager.Render();
	}
	

	//print( format( "\n[Count:%d]", p.Count ) );
}



print("\n\ntitle.txt Running!!!");
