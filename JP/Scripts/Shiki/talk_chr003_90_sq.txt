
Talk <-  // 何を追加してもいいが Finalize() で関数のみになること
{
	IsDone = 0, // ロードできたか
	Count = 0,
	Show = 0, // 描画が有効かどうか
};

// はじまり
function Talk::Initialize()
{
	local p = Talk;
	local fnc = tPAni;

	p.IsDone = 1; // とりあえず成功にしておく

	// 枠生成(ファイル単位)
	p.PA_Ed <- tPAni.CDat();
	p.PA_Manager <- fnc.CAniManager(); // マネージャ生成

	// PAniデータ読み込み
	local sFilePath = "./BattleRes/talk/st_pat";
	
	local sPcFile = format( "%s/chr003/chr003_ed00.pat", sFilePath );
	if( p.PA_Ed.Load( sPcFile ) == 0 )
	{
		//ロード失敗
		print( format( "\n >> %s Load error.", sPcFile ) );
		p.IsDone = 0;
	}
	else
	{
		//ロード成功
		p.Show = 1;//描画を有効にする
		
		//アニメーションを登録する
		local prio = 400; // 全体プライオリティ

		local framestep = 1.0/60; // 1フレーム
		local fWait = 0.0;
		local fr = 0;
		
		// イラスト本体
		fr = p.PA_Manager.Add( p.PA_Ed, "bg0" );
		fr.SetPrio( prio );
		
		fr.AddTag( "wait1" );
		fr.AddFrame( "", framestep );
		fr.AddModeTagJump( "wait1", _PAniFlag_JpGrater, 1 );//Modeが値より大きくなるまでジャンプして非表示
		
		// アルクェイド背中
		fWait = framestep * 5;
		for(local i = 0;i <= 3;i++ )
		{
			local s = format( "bg00_%03d", i );
			fr.AddFrame( s, fWait );
		}
		fWait = framestep * 5;
		for(local i = 100;i <= 101;i++ )
		{
			local s = format( "bg00_%03d", i );
			fr.AddFrame( s, fWait );
		}

		fr.AddSe(214);//SE再生

		fWait = framestep * 5;
		for(local i = 200;i <= 204;i++ )
		{
			local s = format( "bg00_%03d", i );
			fr.AddFrame( s, fWait );
		}
		
		//赤画面へ進んで待機
		fr.AddTag( "wait2" );
		{
			local s = format( "bg00_%03d", 205 );
			fr.AddFrame( s, fWait );
		}
		fr.AddModeTagJump( "wait2", _PAniFlag_JpGrater, 2 );//Modeが値より大きくなるまでジャンプして非表示
		
		fWait = framestep * 30;
		for(local i = 0;i <= 5;i++ )
		{
			local s = format( "bg01_%03d", i );
			fr.AddFrame( s, fWait );
		}
		fr.AddTag("loop2");
		fWait = framestep * 10;
		for(local i = 100;i <= 101;i++ )
		{
			local s = format( "bg01_%03d", i );
			fr.AddFrame( s, fWait );
		}
		fr.AddTagJump("loop2");

		//志貴アップ
		fr = p.PA_Manager.Add( p.PA_Ed, "bg2" );
		fr.AddTag( "wait3" );
		fr.AddFrame( "", framestep );
		fr.AddModeTagJump( "wait3", _PAniFlag_JpGrater, 3 );//Modeが値より大きくなるまでジャンプして非表示
		
		fr.SetPrio( prio+10 );//手前に出す
		fWait = framestep * 10;
		for(local i = 0;i <= 5;i++ )
		{
			local s = format( "bg02_%03d", i );
			fr.AddFrame( s, fWait );
		}
		fr.AddTag("loop3");
		fWait = framestep * 90;
		for(local i = 100;i <= 107;i++ )
		{
			local s = format( "bg02_%03d", i );
			fr.AddFrame( s, fWait );
		}
		fr.AddTagJump("loop3");
	}
}

// 終了処理
function Talk::Finalize()
{
	local p = Talk;
	local fnc = tPAni;

	// アニメマネージャの解放、登録した中身も全部消える
	delete p.PA_Manager;

	// PAniオブジェクトの解放
	delete p.PA_Ed;

	delete Talk;

	print("\nTalk::Finalize()");
}

// 会話スクリプトからコールされる
function Talk::Call(call_no)
{
	print( format( "\nTalk Call:%d", call_no ) );
	
	local p = Talk;
	if( p.IsDone != 0 )
	{
		p.PA_Manager.SetMode(call_no);
	}
}

// 会話スクリプトからウェイト(アニメ終了待ち等の)問い合わせ
function Talk::Wait(call_no)
{
	print( format( "\nTalk Wait:%d", call_no ) );
	return( 0 );	// 0でウェイト終了、それ以外の値でウェイト状態をキープ
}


// スタッフロールが表示される前(ED会話が終わった)のタイミングでコールされる
function Talk::GotoStaffRoll()
{
	print( format( "\nTalk GotoStaffRoll" ) );
	
	local p = Talk;
	p.Show = 0; // 描画を無効にする
}

// 最後の一枚絵が表示される前(スタッフロールが終わった)のタイミングでコールされる
function Talk::GotoEndingLastCG()
{
	print( format( "\nTalk GotoEndingLastCG" ) );
	
	local p = Talk;
	p.Show = 0; // 描画を無効にする
}


//
function Talk::Update()
{
	local p = Talk;
	local fnc = tPAni;

	if( p.Show )
	{
		p.PA_Manager.Update();
		p.Count++;
	}
}

//
function Talk::Render(add_x, add_y)
{
	local p = Talk;
	local fnc = tPAni;
	
	if( p.Show )
	{
		local pa_cx = 1280/2;
		local pa_cy = 720/2;

		p.PA_Ed.SetPosition( pa_cx+add_x, pa_cy+add_y );
		p.PA_Manager.Render();
	}
	

	//print( format( "\n[Count:%d]", p.Count ) );
}



print("\n\ntitle.txt Running!!!");
